# vim: syntax=python

import pandas as pd

# Contains bigwig paths and metadata
cancer_meta = pd.read_csv('manifests/CANCER_SNVDENS.MANIFEST')

scatacseq_meta = pd.read_csv('manifests/SCATACSEQ.MANIFEST')

scrnaseq_meta = pd.read_csv('manifests/SCRNASEQ_EXPRESSION.MANIFEST')


rule fig6_panel_a_twosample:
    input:
        cors1="enrichment/cancer_snvdens/correlation/{any_group1}___A{dot}{corrected}.csv",
        cors2="enrichment/cancer_snvdens/correlation/{any_group2}___A{dot}{corrected}.csv"
    output:
        pdf='fig6{underscore}{corrected}/panel_a_{any_group1}_vs_{any_group2}.{corrected}{dot}pdf',
        svg='fig6{underscore}{corrected}/panel_a_{any_group1}_vs_{any_group2}.{corrected}{dot}svg',
        csv='fig6{underscore}{corrected}/panel_a_{any_group1}_vs_{any_group2}.{corrected}{dot}csv'
    log:
        'fig6{underscore}{corrected}/panel_a_{any_group1}_vs_{any_group2}.{corrected}{dot}log'
    params:
        tag1='{any_group1}',
        tag2='{any_group2}'
    localrule: True
    resources:
        mem_mb=4000,
        localjob=1
    script:
        "scripts/fig6_panel_a.R"


rule fig6_panel_a_onesample:
    input:
        cors1="enrichment/cancer_snvdens/correlation/{any_group}___A.{corrected}{dot}csv",
    output:
        pdf='fig6{underscore}{corrected}/panel_a_{any_group}.{corrected}{dot}pdf',
        svg='fig6{underscore}{corrected}/panel_a_{any_group}.{corrected}{dot}svg',
        csv='fig6{underscore}{corrected}/panel_a_{any_group}.{corrected}{dot}csv'
    log:
        'fig6{underscore}{corrected}/panel_a_{any_group}.{corrected}{dot}log'
    params:
        tag1='{any_group}',
    localrule: True
    resources:
        mem_mb=4000,
        localjob=1
    script:
        "scripts/fig6_panel_a.R"


rule cancer_density_vs_scatacseq:
    input:
        tiles="alignability/genome_tiles/genome_tiles_1000000binsize.bed",
        # quantile is ignored. raw scores used.
        cancer_qbeds=expand('enrichment/cancer_snvdens/quantile/qbed/cancer_snvdens___{tumor}___normdens.1000000binsize_10quantiles.qbed',
            tumor=sorted(set(cancer_meta['tumor']))),
        atac_qbeds=expand('enrichment/scatacseq/quantile/qbed/scatacseq___librarymerged___merged___{celltype}.1000000binsize_10quantiles.qbed',

            celltype=sorted(set(scatacseq_meta['celltype'])))
    output:
        barplot_pdf='fig6/panel_b_barplot.pdf',
        barplot_svg='fig6/panel_b_barplot.svg',
        heatmap_pdf='fig6/panel_b_heatmap.pdf',
        heatmap_svg='fig6/panel_b_heatmap.svg',
        csv='fig6/panel_b.csv'
    log:
        'fig6/panel_b.log'
    resources:
        mem_mb=4000
    script:
        "scripts/fig6_panel_b.R"



# NOTICE! NOTICE!
# We are using scrnaseq_expression_mc02 here because of the 1 MB bins.
# mc08 (meaning only windows covered 80% by a gene) has only ~250 windows
# across the genome that are not NA, because genes only cover about 1/3rd
# of the genome (incl. introns).
rule cancer_density_vs_scrnaseq:
    input:
        tiles="alignability/genome_tiles/genome_tiles_1000000binsize.bed",
        # quantile is ignored. raw scores used.
        cancer_qbeds=expand('enrichment/cancer_snvdens/quantile/qbed/cancer_snvdens___{tumor}___normdens.1000000binsize_10quantiles.qbed',
            tumor=sorted(set(cancer_meta['tumor']))),
        rna_qbeds=expand('enrichment/scrnaseq_expression_mc02/quantile/qbed/scrnaseq___expression___combined___combined___{celltype}.1000000binsize_10quantiles.qbed',

            celltype=sorted(set(scrnaseq_meta['celltype'])))
    output:
        barplot_pdf='fig6/panel_c_barplot.pdf',
        barplot_svg='fig6/panel_c_barplot.svg',
        heatmap_pdf='fig6/panel_c_heatmap.pdf',
        heatmap_svg='fig6/panel_c_heatmap.svg',
        csv='fig6/panel_c.csv'
    log:
        'fig6/panel_c.log'
    resources:
        mem_mb=4000
    script:
        "scripts/fig6_panel_c.R"


rule top_cancer_gene_enrichment:
    input:
        csv="cancer_genes/odds_ratios/{group1}___vs___{group2}.csv"
    output:
        pdf="fig6/panel_d_{group1}_vs_{group2}.pdf",
        svg="fig6/panel_d_{group1}_vs_{group2}.svg",
        supppdf="suppfig5/cancer_gene_ors_{group1}_vs_{group2}.pdf",
        suppsvg="suppfig5/cancer_gene_ors_{group1}_vs_{group2}.svg",
        fullsupppdf="suppfig5/cancer_gene_ors_full_{group1}_vs_{group2}.pdf",
        fullsuppsvg="suppfig5/cancer_gene_ors_full_{group1}_vs_{group2}.svg"
    log:
        "fig6/panel_d_{group1}_vs_{group2}.log"
    resources:
        mem_mb=4000
    script:
        "scripts/fig6_panel_d.R"


rule fig6_panel_d_copytable:
    input:
        "cancer_genes/odds_ratios/{group1}___vs___{group2}.csv"
    output:
        "fig6/panel_d_{group1}_vs_{group2}.csv"
    localrule: True
    resources:
        mem_mb=10,
        localjob=1
    shell:
        """
        cp {input} {output}
        """
