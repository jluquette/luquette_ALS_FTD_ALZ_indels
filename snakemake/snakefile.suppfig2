# vim: syntax=python

print("WARNING: rule orthogonal_tech_comparison uses an Excel spreadsheet with manually entered rates for PTA neurons. Last updated for 56-neuron dataset on 10/18/2023, but MUST be updated MANUALLY if more PTA neurons are sequenced!")
rule suppfig2_orthogonal_tech_comparison:
    input:
        xlsx='external_data/NeuronDuplexSequencingComparison.xlsx'
    output:
        pdf='suppfig2/orthogonal_tech_comparison.pdf'
    log:
        'suppfig2/orthogonal_tech_comparison.log'
    localrule: True
    resources:
        mem_mb=1000,
        localjob=1
    script:
        "scripts/suppfig2_orthogonal_singlecell_techs.R"


rule suppfig2_indel_size_analysis:
    input:
        "tables/pta_neuron___FILTERED_mut___indel_A.csv",
        "tables/pta_oligo___FILTERED_mut___indel_A.csv"
    output:
        "suppfig2/panel_b_indelsize.svg",
        "suppfig2/panel_b_indelsize.pdf",
        "suppfig2/panel_b_indelsize.csv"
    localrule: True
    resources:
        mem_mb=4000,
        localjob=1
    log:
        "suppfig2/panel_b_indelsize.log"
    script:
        "scripts/suppfig2_plot_indel_sizes.R"


rule suppfig2_indel_aging_curves:
    input:
        meta='metadata/sample_metadata.csv',
        muts='scan2/{group}_mutations.UNFILTERED.txt'
    output:
        data_csv="suppfig2/panel_a_{group}_ins_del_aging_rates.csv",
        pdf="suppfig2/panel_a_{group}_ins_del_aging_rates.pdf",
        models_csv="suppfig2/panel_a_{group}_ins_del_aging_rates_models.csv"
    params:
        color=lambda wildcards: config['group_colors'][wildcards.group]
    log:
        "suppfig2/panel_a_{group}_ins_del_aging_rates.log"
    localrule: True
    resources:
        mem_mb=1000,
        localjob=1
    script:
        "scripts/suppfig2_ins_del_separate_aging_rates.R"


rule suppfig2_plot_bed_sensitivity_and_covariates:
    input:
        sens=expand("enrichment/sensitivity/{group}.summary.txt",
            group=[ 'pta_neuron', 'pta_oligo', 'mda_sox10', 'mda_gfap' ])
    output:
        pdf='suppfig2_corrected/bed_sensitivity_panels.pdf'
    params:
        datasources='gtex',
        group_colors=','.join(g + "=" + config['group_colors'][g] for g in ['pta_neuron', 'pta_oligo', 'mda_sox10', 'mda_gfap'])
    log:
        'suppfig2_corrected/bed_sensitivity_panels.log'
    threads: 1
    localrule: True
    resources:
        mem_mb=1000,
        localjob=1
    script:
        "scripts/suppfig3_bed_sensitivity.R"


rule individual_gene_enrichment:
    input:
        neuron="enrichment/gtex_genes/bed_regions/pta_neuron___AB/bedenrich/SUMMARY.rda",
        oligo="enrichment/gtex_genes/bed_regions/pta_oligo___AB/bedenrich/SUMMARY.rda"
    output:
        csv="suppfig2/panel_d_genes.csv",
        pdf="suppfig2/panel_d_genes.pdf",
        svg="suppfig2/panel_d_genes.svg",
        jpeg="suppfig2/panel_d_genes.jpeg"
    localrule: True
    resources:
        mem_mb=8000,
        localjob=1
    log:
        "suppfig2/panel_d_genes.log"
    script:
        "scripts/suppfig2_panel_d_genes.R"
