# vim: syntax=python

rule umap_plots:
    input:
        lambda wildcards: "input/any___scatacseq_umap___none.csv" if wildcards.panel_letter == 'c' else "input/any___scrnaseq_umap___none.csv"
    output:
        expand("fig4/panel_{{panel_letter}}_umap_plot.{ext}",
            ext=[ 'svg', 'pdf', 'jpeg' ])
    resources:
        mem=24000
    log:
        "fig4/panel_{panel_letter}_umap_plot.log"
    script:
        "scripts/plot_umap.R"


rule many_enrichment_analyses:
    input:
        # would like to expand() this but also want to keep names of inputs
        neuron_scrnaseq="enrichment/scrnaseq_expression_mc08/quantile/neuron{half}___AB.10quantiles.csv",
        neuron_scatacseq="enrichment/scatacseq/quantile/neuron{half}___AB.10quantiles.csv",
        neuron_repliseq="enrichment/repliseq/quantile/neuron{half}___AB.10quantiles.csv",
        neuron_histone="enrichment/roadmap_histone_signal_brain/quantile/neuron{half}___AB.10quantiles.csv",
        oligo_scrnaseq="enrichment/scrnaseq_expression_mc08/quantile/oligo{half}___AB.10quantiles.csv",
        oligo_scatacseq="enrichment/scatacseq/quantile/oligo{half}___AB.10quantiles.csv",
        oligo_repliseq="enrichment/repliseq/quantile/oligo{half}___AB.10quantiles.csv",
        oligo_histone="enrichment/roadmap_histone_signal_brain/quantile/oligo{half}___AB.10quantiles.csv"
    output:
        pdf="fig4/panels_cdefg_enrichment_analyses{half}.pdf",
        svg="fig4/panels_cdefg_enrichment_analyses{half}.svg",
        csv="fig4/panels_cdefg_enrichment_analyses{half}.csv"
    log:
        "fig4/panels_cdefg_enrichment_analyses{half}.log"
    resources:
        mem=4000
    script:
        "scripts/fig4_panels_cdefg.R"


rule chromhmm_states:
    input:
        neuron_snv="enrichment/roadmap_chromhmm_brain/bed_regions/neuron___AB/bedenrich/SUMMARY.rda",
        neuron_indel="enrichment/roadmap_chromhmm_brain/bed_regions/neuron___indel_AB/bedenrich/SUMMARY.rda",
        oligo_snv="enrichment/roadmap_chromhmm_brain/bed_regions/oligo___AB/bedenrich/SUMMARY.rda",
        oligo_indel="enrichment/roadmap_chromhmm_brain/bed_regions/oligo___indel_AB/bedenrich/SUMMARY.rda"
    output:
        svg="fig4/panel_h_chromhmm.svg",
        pdf="fig4/panel_h_chromhmm.pdf",
        csv="fig4/panel_h_chromhmm.csv"
    params:
        signal_to_plot='eid=E073',
        # 3_TssFlnk, 10_,11_,12_ (bivalent states) are all tiny genomic regions, creating
        # huge error bars that make the rest of the plot difficult to read.
        features="1_TssA,2_TssAFlnk,4_Tx,5_TxWk,6_EnhG,7_Enh,8_ZNF/Rpts,9_Het,13_ReprPC,14_ReprPCWk,15_Quies"
    resources:
        mem=1000
    log:
        "fig4/panel_h_chromhmm.log"
    script:
        "scripts/barplot_enrich_new.R"


rule nott_enhancers_and_promoters:
    input:
        neuron_snv="enrichment/nott/bed_regions/neuron___AB/bedenrich/SUMMARY.rda",
        neuron_indel="enrichment/nott/bed_regions/neuron___indel_AB/bedenrich/SUMMARY.rda",
        oligo_snv="enrichment/nott/bed_regions/oligo___AB/bedenrich/SUMMARY.rda",
        oligo_indel="enrichment/nott/bed_regions/oligo___indel_AB/bedenrich/SUMMARY.rda"
    output:
        svg="fig4/panel_i_nott_enhprom.svg",
        pdf="fig4/panel_i_nott_enhprom.pdf",
        csv="fig4/panel_i_nott_enhprom.csv"
    params:
        signal_to_plot='datasource=nott_enhprom',
        features="astrocyte_enhancer,neuron_enhancer,oligo_enhancer,microglia_enhancer,astrocyte_promoter,neuron_promoter,oligo_promoter,microglia_promoter"
    resources:
        mem=1000
    log:
        "fig4/panel_i_nott_enhprom.log"
    script:
        "scripts/barplot_enrich_new.R"


####################################################################################
# UNUSED
####################################################################################

rule fig4_suppl_boca2_peaks:
    input:
        neuron_snv="enrichment/boca2/bed_regions/neuron___AB/bedenrich/SUMMARY.rda",
        neuron_indel="enrichment/boca2/bed_regions/neuron___indel_AB/bedenrich/SUMMARY.rda",
        oligo_snv="enrichment/boca2/bed_regions/oligo___AB/bedenrich/SUMMARY.rda",
        oligo_indel="enrichment/boca2/bed_regions/oligo___indel_AB/bedenrich/SUMMARY.rda"
    output:
        svg="fig4/suppl_boca2_peaks.svg",
        pdf="fig4/suppl_boca2_peaks.pdf",
        csv="fig4/suppl_boca2_peaks.csv"
    params:
        signal_to_plot='region=DLPFC',
        features="GABA,GLU,OLIG,MGAS"
    resources:
        mem=1000
    log:
        "fig4/suppl_boca2_peaks_barplot_new.log"
    script:
        "scripts/barplot_enrich_new.R"


rule fig4_suppl_dna_repair_hotspots:
    input:
        neuron_snv="enrichment/dna_repair_hotspots/bed_regions/neuron___AB/bedenrich/SUMMARY.rda",
        neuron_indel="enrichment/dna_repair_hotspots/bed_regions/neuron___indel_AB/bedenrich/SUMMARY.rda",
        oligo_snv="enrichment/dna_repair_hotspots/bed_regions/oligo___AB/bedenrich/SUMMARY.rda",
        oligo_indel="enrichment/dna_repair_hotspots/bed_regions/oligo___indel_AB/bedenrich/SUMMARY.rda"
    output:
        svg="fig4/suppl_dna_repair_hotspots.svg",
        pdf="fig4/suppl_dna_repair_hotspots.pdf",
        csv="fig4/suppl_dna_repair_hotspots.csv"
    params:
        signal_to_plot='datasource=dnarepair',
        features="SAR-seq,Repair-seq"
    resources:
        mem=1000
    log:
        "fig4/suppl_dna_repair_hotspots_barplot_new.log"
    script:
        "scripts/barplot_enrich_new.R"


rule fig4_suppl_hic_tads:
    input:
        neuron_snv="enrichment/hic_tads/bed_regions/neuron___AB/bedenrich/SUMMARY.rda",
        neuron_indel="enrichment/hic_tads/bed_regions/neuron___indel_AB/bedenrich/SUMMARY.rda",
        oligo_snv="enrichment/hic_tads/bed_regions/oligo___AB/bedenrich/SUMMARY.rda",
        oligo_indel="enrichment/hic_tads/bed_regions/oligo___indel_AB/bedenrich/SUMMARY.rda"
    output:
        svg="fig4/suppl_hic_tads.svg",
        pdf="fig4/suppl_hic_tads.pdf",
        csv="fig4/suppl_hic_tads.csv"
    params:
        signal_to_plot='datasource=hic_tads',
        features="ME45_Adult_Rep1_A,ME45_Adult_Rep1_B"
    resources:
        mem=1000
    log:
        "fig4/suppl_hic_tads_barplot_new.log"
    script:
        "scripts/barplot_enrich_new.R"


rule fig4_foldchange:
    input:
        neuron_atac="enrichment/scatacseq_foldchange/quantile/neuron___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        oligo_atac="enrichment/scatacseq_foldchange/quantile/oligo___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        neuron_rna="enrichment/scrnaseq_expression_mc08_foldchange/quantile/neuron___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        oligo_rna="enrichment/scrnaseq_expression_mc08_foldchange/quantile/oligo___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda"
    output:
        pdf='fig4/rna_atac_foldchange.pdf',
        svg='fig4/rna_atac_foldchange.svg',
        csv='fig4/rna_atac_foldchange.csv'
    log:
        'fig4/rna_atac_foldchange.log'
    resources:
        mem=4000
    script:
        "scripts/fig4_rna_atac_foldchange.R"
