# vim: syntax=python

import pandas as pd

# Contains bigwig paths and metadata
cancer_meta = pd.read_csv('manifests/CANCER_SNVDENS.MANIFEST')

scatacseq_meta = pd.read_csv('manifests/SCATACSEQ.MANIFEST')

rule fig4a:
    input:
        neuron_muts="input/neuron___mut___A.rda",
        oligo_muts="input/oligo___mut___A.rda",
        tiles="alignability/genome_tiles/genome_tiles_1000000binsize.bed",
        # quantile is ignored. raw scores used.
        cancer_qbeds=expand('enrichment/cancer_snvdens/quantile/qbed/cancer_snvdens___{tumor}___normdens.1000000binsize_10quantiles.qbed',
            tumor=sorted(set(cancer_meta['tumor'])))
    output:
        pdf='fig4/panel_a.pdf',
        svg='fig4/panel_a.svg',
        csv='fig4/panel_a.csv'
    log:
        'fig4/panel_a.log'
    resources:
        mem=4000
    script:
        "scripts/fig4_panel_a.R"


rule fig4b:
    input:
        tiles="alignability/genome_tiles/genome_tiles_1000000binsize.bed",
        # quantile is ignored. raw scores used.
        cancer_qbeds=expand('enrichment/cancer_snvdens/quantile/qbed/cancer_snvdens___{tumor}___normdens.1000000binsize_10quantiles.qbed',
            tumor=sorted(set(cancer_meta['tumor']))),
        atac_qbeds=expand('enrichment/scatacseq/quantile/qbed/scatacseq___librarymerged___merged___{celltype}.1000000binsize_10quantiles.qbed',

            celltype=sorted(set(scatacseq_meta['celltype'])))
    output:
        barplot_pdf='fig4/panel_b_barplot.pdf',
        barplot_svg='fig4/panel_b_barplot.svg',
        heatmap_pdf='fig4/panel_b_heatmap.pdf',
        heatmap_svg='fig4/panel_b_heatmap.svg',
        csv='fig4/panel_b.csv'
    log:
        'fig4/panel_b.log'
    resources:
        mem=4000
    script:
        "scripts/fig4_panel_b.R"


rule fig4c:
    input:
        neuron_atac="enrichment/scatacseq_foldchange/quantile/neuron___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        oligo_atac="enrichment/scatacseq_foldchange/quantile/oligo___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        neuron_rna="enrichment/scrnaseq_expression_mc08_foldchange/quantile/neuron___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda",
        oligo_rna="enrichment/scrnaseq_expression_mc08_foldchange/quantile/oligo___AB/qbedenrich/tiled_1000binsize_10quantiles.SUMMARY.rda"
    output:
        pdf='fig4/panel_c.pdf',
        svg='fig4/panel_c.svg',
        csv='fig4/panel_c.csv'
    log:
        'fig4/panel_c.log'
    resources:
        mem=8000
    script:
        "scripts/fig4_panel_c.R"
