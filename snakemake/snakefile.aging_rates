# vim: syntax=python

# Allows mutation burdens to be collected from SCAN2 objects OR
# directly from tables (such as previous SCAN2 analyses).
rule collect_mut_burdens:
    input:
        objects=lambda wildcards: config['aging_groups'][wildcards.celltype],
        meta='metadata/sample_metadata.csv'
    output:
        csv="aging_rates/{celltype}___mutburden___{qualtype}.csv"
    params:
        muttype=lambda wildcards: 'indel' if wildcards.qualtype.startswith('indel_') else 'snv',
    resources:
        mem_mb=1000
    log:
        "aging_rates/{celltype}___mutburden___{qualtype}.log"
    script:
        "scripts/make_mutburden_tables.R"


# Fit a linear model to each celltype
# Also fits a joint linear model across all celltypes_to_compute to statistically
# test for difference in rates between celltypes.
rule model_aging_rates:
    input:
        expand("aging_rates/{celltype}___mutburden___{{qualtype}}.csv",
            celltype=config['aging_groups_to_model'])
    output:
        svg="aging_rates/all___mutburden_models___{qualtype}.svg",
        pdf="aging_rates/all___mutburden_models___{qualtype}.pdf",
        models="aging_rates/all___mutburden_models___{qualtype}.csv",
        burdens="aging_rates/all___mutburden___{qualtype}.csv"
    resources:
        mem_mb=4000
    log:
        "aging_rates/all___mutburden_models___{qualtype}.log"
    script:
        "scripts/model_aging_rates.R"
