########################################################################
# Rizzardi et al brain DNA methylation
########################################################################
enrichment_rizzardi_config = dict(
    **{ 'output_dir': 'enrichment/rizzardi',
        'SIGNAL_MANIFEST': 'manifests/RIZZARDI.MANIFEST' },
    **enrichment_config
)

module enrichment_rizzardi:
    snakefile: "snakefile.enrichment"
    config: enrichment_rizzardi_config

use rule * from enrichment_rizzardi  as enrichment_rizzardi_*

# Also slightly large; increase memory.
use rule make_qbed_from_bigwig from enrichment_rizzardi as enrichment_rizzardi_make_qbed_from_bigwig with:
    resources:
        mem=15000

use rule enrichment_plot from enrichment_rizzardi as enrichment_rizzardi_enrichment_plot with:
    params:
        ignore='enrichment_grid_R_ignore_none',
        group='region,methyltype,smoothing'
    output:
        expand('enrichment/rizzardi/quantile/{{mutclass}}.{{nquantiles}}quantiles.{output}',
            output=[ 'svg', 'pdf', 'csv' ])
    log:
        'enrichment/rizzardi/quantile/{mutclass}.{nquantiles}quantiles.log'


########################################################################
# BOCA brain region ATAC-seq
########################################################################
enrichment_boca_config = dict(
    **{ 'output_dir': 'enrichment/boca',
        'SIGNAL_MANIFEST': 'manifests/BOCA.MANIFEST' },
    **enrichment_config
)

module enrichment_boca:
    snakefile: "snakefile.enrichment"
    config: enrichment_boca_config

use rule * from enrichment_boca as enrichment_boca_*

# BOCA ATAC tracks are ~10-fold larger than most others, so give them a
# bit more RAM.
use rule make_qbed_from_bigwig from enrichment_boca as enrichment_boca_make_qbed_from_bigwig with:
    resources:
        mem=15000

use rule enrichment_plot from enrichment_boca as enrichment_boca_enrichment_plot with:
    params:
        ignore='enrichment_grid_R_ignore_none',
        group='datasource,celltype,region'
    output:
        expand('enrichment/boca/quantile/{{mutclass}}.{{nquantiles}}quantiles.{output}',
            output=[ 'svg', 'pdf', 'csv' ])
    log:
        'enrichment/boca/quantile/{mutclass}.{nquantiles}quantiles.log'


########################################################################
# Sequncing depth
########################################################################
enrichment_depth_config = dict(
    **{ 'output_dir': 'enrichment/depth',
        'SIGNAL_MANIFEST': 'manifests/DEPTH.MANIFEST' },
    **enrichment_config
)

module enrichment_depth:
    snakefile: "snakefile.enrichment"
    config: enrichment_depth_config

use rule * from enrichment_depth  as enrichment_depth_*

use rule enrichment_plot from enrichment_depth as enrichment_depth_enrichment_plot with:
    params:
        ignore='enrichment_grid_R_ignore_none',
        group='celltype'
    output:
        expand('enrichment/depth/quantile/{{mutclass}}.{{nquantiles}}quantiles.{output}',
            output=[ 'svg', 'pdf', 'csv' ])
    log:
        'enrichment/depth/quantile/{mutclass}.{nquantiles}quantiles.log'
########################################################################
# Roadmap DNA methylation
########################################################################
enrichment_roadmap_dnamethyl_config = dict(
    **{ 'output_dir': 'enrichment/roadmap_dnamethyl',
        'SIGNAL_MANIFEST': 'manifests/ROADMAP_DNAMETHYL.MANIFEST' },
    **enrichment_config
)

module enrichment_roadmap_dnamethyl:
    snakefile: "snakefile.enrichment"
    config: enrichment_roadmap_dnamethyl_config

use rule * from enrichment_roadmap_dnamethyl  as enrichment_roadmap_dnamethyl_*

use rule enrichment_plot from enrichment_roadmap_dnamethyl as enrichment_roadmap_dnamethyl_enrichment_plot with:
    params:
        ignore='enrichment_grid_R_ignore_none',
        group='experiment_type,signal_type'
    output:
        expand('enrichment/roadmap_dnamethyl/quantile/{{mutclass}}.{{nquantiles}}quantiles.{output}',
            output=[ 'svg', 'pdf', 'csv' ])
    log:
        'enrichment/roadmap_dnamethyl/quantile/{mutclass}.{nquantiles}quantiles.log'

########################################################################
# Roadmap epigenomics histone mark narrow peak calls
########################################################################
enrichment_roadmap_histone_narrowpeak_config = dict(
    **{ 'output_dir': 'enrichment/roadmap_histone_narrowpeak',
        'SIGNAL_MANIFEST': 'manifests/ROADMAP_HISTONE_NARROWPEAK.MANIFEST' },
    **enrichment_config
)

module enrichment_roadmap_histone_narrowpeak:
    snakefile: "snakefile.bed_enrichment"
    config: enrichment_roadmap_histone_narrowpeak_config

use rule * from enrichment_roadmap_histone_narrowpeak as enrichment_roadmap_histone_narrowpeak_*

use rule enrichment_bed_plot from enrichment_roadmap_histone_narrowpeak as enrichment_roadmap_histone_narrowpeak_enrichment_bed_plot with:
    params:
        ignore='enrichment_grid_R_ignore_none',
        xlab='eid',
        xgroup='mark',
        ygroup='datasource',
        highlight='eid=E073'
    output:
        expand('enrichment/roadmap_histone_narrowpeak/{{mutclass}}.{output}',
            output=[ 'svg', 'pdf', 'csv' ])
    log:
        'enrichment/roadmap_histone_narrowpeak/{mutclass}.log'
